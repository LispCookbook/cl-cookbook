<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<!-- 	$Header$	 -->
<html> <head>
<title>The Common Lisp Cookbook - Foreign Function Interfaces</title>
<style type="text/css">
pre { margin-left:20px; background-color:#e0e0e0 }
</style>

</head>

<body bgcolor=white>
<h1><a href="index.html">The Common Lisp Cookbook</a> - Foreign Function Interfaces</h1>


<h2>Contents</h2>
<ul>
<li><a href="#clisp-gethost">Example: Calling 'gethostname' from CLISP</a>
<li><a href="#alisp-gethost">Example: Calling 'gethostname' from Allegro CL</a>
</ul>

The ANSI Common Lisp standard doesn't mention this topic. So almost
everything that can be said here depends on your OS and your
implementation.


<a name="clisp-gethost">
<h3>Example: Calling 'gethostname' from CLISP</h3>

Note: You should read the <a
href="http://clisp.sourceforge.net/impnotes.html#dffi">relevant
chapter</a> from the CLISP implementation notes before you proceed.
<p>

<code>int gethostname(char *name, int len)</code>  follows a typical
pattern of C &quot;out&quot;-parameter convention - it expects a
pointer to a buffer it's going to fill. So you must view this
parameter as either <code>:OUT</code> or <code>:IN-OUT</code>. 

Additionaly, one must tell the function the size of the buffer. Here
<code>len</code> is just an <code>:IN</code> parameter. Sometimes this
will be an <code>:IN-OUT</code> parameter, returning the number of
bytes actually filled in. 
<p>

So <code>name</code> is actually a pointer to an array of up to
<code>len</code> characters, regardless of what the poor
&quot;<code>char *</code>&quot; C prototype says, to be used like a C
string (0-termination).

How many elements are in the array? Luckily, in our case, you can find
it out without calculating the <code>sizeof()</code> a C structure. It's a hostname
that will be returned. The Solaris 2.x manpage says &quot;Host names are
limited to MAXHOSTNAMELEN characters, currently 256.&quot;
<p>

Also, in the present example, you can use allocation <code>:ALLOCA</code>, like you'd do
in C: stack-allocate a temporary. Why make things worse when using
Lisp than when using C?
<p>

This yields the following useful signature for your foreign function:

<PRE>
(ffi:def-c-call-out gethostname
  (:arguments (name (ffi:c-ptr (ffi:c-array-max ffi:char 256))
		    :out :alloca)
	      (len ffi:int))
  ;; (:return-type BOOLEAN) could have been used here
  ;; (Solaris says it's either 0 or -1).
  (:return-type ffi:int))

(defun myhostname ()
  (multiple-value-bind (success name)
      ;; :OUT or :IN-OUT parameters are returned via multiple values
      (gethostname 256)
    (if (zerop success)
	(subseq name 0 (position #\null name))
	(error ... ; errno may be set
          ...))))
(defvar hostname (myhostname))
</PRE>

Possibly <code>SUBSEQ</code> and <code>POSITION</code> are
superfluous, thanks to <code>C-ARRAY-MAX</code> as opposed to
<code>C-ARRAY</code>: 

<PRE>
(defun myhostname ()
  (multiple-value-bind (success name)
      ;; :out or :in-out parameters are returned via multiple values
      (gethostname 256)
    (if (zerop success) name
	(error ... ; errno may be set
          ...))))
</PRE>


<a name="alisp-gethost"></a>
<h3>Example: Calling 'gethostname' from Allegro CL</h3>

This is how the same example above would be written in Allegro
Common Lisp version 6 and above. ACL doesn't explicitely
distinguish between <code>input</code> and <code>output</code>
arguments. The way to declare an argument as <code>output</code>
(i.e., modifiable by C) is to use an array, since arrays are
passed by reference and C therefore receives a pointer to a
memory location (which is what it expects). In this case things
are made even easier by the fact that <code>gethostname()</code> expects 
an array of char, and a <code>SIMPLE-ARRAY</code> of
<code>CHARACTER</code> represents essentially the same thing in
Lisp. The foreign function definition is therefore the following:

<pre>
(def-foreign-call (c-get-hostname "gethostname")
	 ((name (* :char) (simple-array 'character (*)))
	  (len :int integer))
  :returning :int)
</pre>

Let's read this line by line: this form defines a Lisp function
called <code>C-GET-HOSTNAME</code> that calls the C function
<code>gethostname()</code>. It takes two arguments: the first one,
called <code>NAME</code>, is a pointer to a char (<code>*char</code>
in C), and a <code>SIMPLE-ARRAY</code> of characters in Lisp; the
second one is called <code>LEN</code>, and is an integer. The function
returns an integer value. 
<p>

And now the Lisp side:

<pre>
(defun get-hostname ()
  (let* ((name (make-array 256 :element-type 'character))
	     (result (c-get-hostname name 256)))
    (if (zerop result)
	    (let ((pos (position #\null name)))
	      (subseq name 0 pos))
	  (error "gethostname() failed."))))
</pre>

This function creates the <code>NAME</code> array, calls
<code>C-GET-HOSTNAME</code> to fill it and then checks the returned
value. If the value is zero, then the call was successful, and we
return the contents of <code>NAME</code> up to the first 0 character
(the string terminator in C), otherwise we signal an error. Note that,
unlike the previous example, we allocate the string in Lisp, and we
rely on the Lisp garbage collector to get rid of it after the function
terminates. Here is a usage example:

<pre>
* (get-hostname)
"terminus"
</pre>

Working with strings is, in general, easier than the previous
example showed. Let's say you want to call <code>getenv()</code>
from Lisp to access the value of an environment
variable. <code>getenv()</code> takes a string argument (the
variable name) and returns another string (the variable
value). To be more precise, the argument is a <i>pointer</i> to
a sequence of characters that should have been allocated by the 
caller, and the return value is a pointer to an already-existing 
sequence of chars (in the environment). Here is the definition
of <code>C-GETENV</code>:

<pre>
(def-foreign-call (c-getenv "getenv")
    ((var (* :char) string))
  :returning :int
  :strings-convert t)
</pre>

The argument in this case is still a pointer to char in C, but
we can declare it a <code>STRING</code> to Lisp. The return value is a
pointer, so we declare it as integer. Finally, the
<code>:STRINGS-CONVERT</code> keyword argument specifies that ACL
should automatically translate the Lisp string passed as the first
argument into a C string. Here is how it's used:

<pre>
* (c-getenv "SHELL")
-1073742215
</pre>

If you are surprised by the return value, just remember that
<code>C-GETENV</code> returns a pointer, and we must tell Lisp how to
interpret the contents of the memory location pointed to by it. Since
in this case we know that it will point to a C string, we can use the
<code>FF:NATIVE-TO-STRING</code> function to convert it to a Lisp string:

<pre>
* (native-to-string (c-getenv "SHELL"))
"/bin/tcsh"
9
9
</pre>

(The second and third values are the number of characters and
bytes copied, respectively). One caveat: if you ask for the value
of a non-existent variable, <code>C-GETENV</code> will return 0, and
<code>NATIVE-TO-STRING</code> will fail. So a safer example would be:

<pre>
* (let ((ptr (c-getenv "NOSUCHVAR")))
    (unless (zerop ptr)
      (native-to-string ptr)))
NIL
</pre>

<hr>
<a href="license.html">Copyright</a> &copy; 2002-2007 The Common Lisp Cookbook Project
<address>http://cl-cookbook.sourceforge.net/</address>
<br>$Header$
</body> </html>
