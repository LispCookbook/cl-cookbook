<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<!-- 	$Header$	 -->
<html> <head>
<title>The Common Lisp Cookbook - Input/Output</title>
<style type="text/css">
pre { margin-left:20px; background-color:#e0e0e0 }
</style>

</head>

<body bgcolor=white>
<h1><a href="index.html">The Common Lisp Cookbook</a> - Input/Output</h1>


<h2>Contents</h2>
<ul>
<li><a href="#redir">Redirecting the Standard Output of your Program</a>
<li><a href="#faith">Faithful Output with Character Streams</a>
<li><a href="#bulk">Fast Bulk I/O</a>
</ul>


<a name="redir"></a>
<h3>Redirecting the Standard Output of your Program</h3>

You do it like this:

<pre>
(let ((*standard-output* <em>&lt;some form generating a stream&gt;</em>))
  <em>...</em>)
</pre>

Because <a href="http://www.lispworks.com/documentation/HyperSpec/Body/v_debug_.htm"><code>*STANDARD-OUTPUT*</code></a>
is a dynamic variable, all references to it during execution of the
body of the <code>LET</code> form refer to the stream that you bound
it to.  After exiting the <code>LET</code> form, the old value of
<code>*STANDARD-OUTPUT*</code> is restored, no matter if the exit was
by normal execution, a <code>RETURN-FROM</code> leaving the whole
function, an exception, or what-have-you.  (This is, incidentally, why
global variables lose much of their brokenness in Common Lisp compared
to other languages: since they can be bound for the execution of a
specific form without the risk of losing their former value after the
form has finished, their use is quite safe; they act much like
additional parameters that are passed to every function.)
<p>

If the output of the program should go to a file, you can do the
following:

<pre>
(with-open-file (*standard-output* "somefile.dat" :direction :output
                                   :if-exists :supersede)
  <em>...</em>)
</pre>
  
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/m_w_open.htm"><code>WITH-OPEN-FILE</code></a>
opens the file - creating it if necessary - binds 
<code>*STANDARD-OUTPUT*</code>, executes its body, closes the file,
and restores <code>*STANDARD-OUTPUT*</code> to its former value. It
doesn't get more comfortable than this!


<a name="faith"></a>
<h3>Faithful Output with Character Streams</h3>

By <em>faithful output</em> I mean that characters with codes between 0 and 255 will be
written out as is. It means, that I can <code>(PRINC (CODE-CHAR 0..255) s)</code> to a stream and
expect 8-bit bytes to be written out, which is not obvious in the
times of Unicode and 16 or 32 bit character representations.

It does <em>not</em> require that the characters &auml;, &szlig;, or &thorn; must
have their <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_char_c.htm"><code>CHAR-CODE</code></a>
in the range 0..255 - the implementation is free 
to use any code. But it does require that no <code>#\Newline</code> to CRLF translation takes place, among
others.
<p>

Common Lisp has a long tradition of distinguishing character from byte
(binary) I/O, e.g. <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_rd_by.htm"><code>READ-BYTE</code></a>
and <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_rd_cha.htm"><code>READ-CHAR</code></a>
are in the standard. Some implementations let both functions be called
interchangeably. Others allow either one or the other. 
(The <a href="http://ww.telent.net/cliki/simple-stream">simple stream proposal</a>
defines the notion of a <em>bivalent stream</em> where both are
possible.)
<p>

Varying element-types are useful as some protocols rely on the ability
to send 8-Bit output on a channel. E.g. with HTTP, the header is
normally ASCII and ought to use CRLF as line terminators, whereas the
body can have the MIME type application/octet-stream, where CRLF
translation would destroy the data. (This is how the Netscape
browser on MS-Windows destroys data sent by incorrectly configured
Webservers which declare unknown files as having MIME type text/plain
- the default in most Apache configurations).
<br>&nbsp;<br>

What follows is a list of implementation dependent choices and
behaviours and some code to experiment.

<ul>
<li><b>CLISP:</b>
On CLISP, faithful output is possible using
<pre>
:external-format
(ext:make-encoding :charset 'charset:iso-8859-1 :line-terminator :unix)
</pre>
You can also use <code>(SETF (STREAM-ELEMENT-TYPE F) '(UNSIGNED-BYTE 8))</code>,
where the ability to <code>SETF</code> is a CLISP-specific extension.

Using <code>:EXTERNAL-FORMAT :UNIX</code> will cause portability problems, since the default character set on MS-Windows is <code>CHARSET:CP1252</code>.
<code>CHARSET:CP1252</code> doesn't allow output of e.g. <code>(CODE-CHAR #x81)</code>:
<pre>
;*** - Character #\u0080 cannot be represented in the character set CHARSET:CP1252
</pre>

Characters with code &gt; 127 cannot be represented in ASCII:
<pre>
;*** - Character #\u0080 cannot be represented in the character set CHARSET:ASCII
</pre>
<li><b>CMUCL:</b>
<code>:EXTERNAL-FORMAT :DEFAULT</code> (untested)
- no unicode, so probably no problems.
<li><b>AllegroCL:</b>
<code>#+(AND ALLEGRO UNIX) :DEFAULT</code> (untested)
- seems enough on UNIX, but would not work on the MS-Windows port of AllegroCL.
<li><b>LispWorks:</b>
<code>:EXTERNAL-FORMAT '(:LATIN-1 :EOL-STYLE :LF)</code> (confirmed by Marc Battyani)
</ul>

Here's some sample code to play with:
<pre>
(defvar *unicode-test-file* "faithtest-out.txt")

(defun generate-256 (&key (filename *unicode-test-file*)
			  #+CLISP (charset 'charset:iso-8859-1)
                          external-format)
  (let ((e (or external-format
	       #+CLISP (ext:make-encoding :charset charset :line-terminator :unix))))
    (describe e)
    (with-open-file (f filename :direction :output
		     :external-format e)
      (write-sequence
        (loop with s = (make-string 256)
	      for i from 0 to 255
	      do (setf (char s i) (code-char i))
	      finally (return s))
       f)
      (file-position f))))

;(generate-256 :external-format :default)
;#+CLISP (generate-256 :external-format :unix)
;#+CLISP (generate-256 :external-format 'charset:ascii)
;(generate-256)

(defun check-256 (&optional (filename *unicode-test-file*))
  (with-open-file (f filename :direction :input
		     :element-type '(unsigned-byte 8))
    (loop for i from 0
	  for c = (read-byte f nil nil)
	  while c
	  unless (= c i)
	  do (format t "~&Position ~D found ~D(#x~X)." i c c)
	  when (and (= i 33) (= c 32))
	  do (let ((c (read-byte f)))
	       (format t "~&Resync back 1 byte ~D(#x~X) - cause CRLF?." c c) ))
    (file-length f)))

#| CLISP
(check-256 *unicode-test-file*)
(progn (generate-256 :external-format :unix) (check-256))
; uses UTF-8 -&gt; 385 bytes

(progn (generate-256 :charset 'charset:iso-8859-1) (check-256))

(progn (generate-256 :external-format :default) (check-256))
; uses UTF-8 + CRLF(on MS-Windows) -&gt; 387 bytes

(progn (generate-256 :external-format
  (ext:make-encoding :charset 'charset:iso-8859-1 :line-terminator :mac)) (check-256))
(progn (generate-256 :external-format
  (ext:make-encoding :charset 'charset:iso-8859-1 :line-terminator :dos)) (check-256))
|#
</pre>

<a name="bulk"></a>
<h3>Fast Bulk I/O</h3>

If you need to copy a lot of data and the source and destination are
both streams (of the same <a href="http://www.lispworks.com/documentation/HyperSpec/Body/26_glo_e.htm#element_type">element type</a>), it's very fast to use <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_rd_seq.htm"><code>READ-SEQUENCE</code></a> and <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_wr_seq.htm"><code>WRITE-SEQUENCE</code></a>:

<pre>
(let ((buf (make-array 4096 :element-type (stream-element-type input-stream)))
  (loop for pos = (read-sequence input-stream)
        while (plusp pos)
        do (write-sequence buf output-stream :end pos))))
</pre>
  
<hr>
<a href="license.html">Copyright</a> &copy; 2002-2007 The Common Lisp Cookbook Project
<address>http://cl-cookbook.sourceforge.net/</address>
<br>$Header$
</body> </html>
