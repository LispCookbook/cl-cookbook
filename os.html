<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<!-- 	$Header$	 -->
<html> <head>
<title>The Common Lisp Cookbook - Interfacing with your OS</title>
<style type="text/css">
pre { margin-left:20px; background-color:#e0e0e0 }
</style>

</head>

<body bgcolor=white>
<h1><a href="index.html">The Common Lisp Cookbook</a> - Interfacing with your OS</h1>


<h2>Contents</h2>

<ul>
<li><a href="#env">Accessing Environment Variables</a>
<li><a href="#accessing-command-line">Accessing Command Line Arguments</a>
<li><a href="#fork-cmucl">Forking with CMUCL</a>
</ul>

The ANSI Common Lisp standard doesn't mention this topic. (Keep in
mind that it was written at a time where
<a href="http://kogs-www.informatik.uni-hamburg.de/~moeller/symbolics-info/symbolics.html">Lisp
Machines</a> were at their peak. On these boxes Lisp <em>was</em> your
operating system!) So almost everything that can be said here depends
on your OS and your implementation.


<a name="env"></a>
<h3>Accessing Environment variables</h3>

Here's a function that'll allow you to look at Unix/Linux environment
variables on a lot of different CL implementations:

<pre>
* (defun my-getenv (name &optional default)
    #+CMU
    (let ((x (assoc name ext:*environment-list*
                    :test #'string=)))
      (if x (cdr x) default))
    #-CMU
    (or
     #+Allegro (sys:getenv name)
     #+CLISP (ext:getenv name)
     #+ECL (si:getenv name)
     #+SBCL (sb-unix::posix-getenv name)
     #+LISPWORKS (lispworks:environment-variable name)
     default))
MY-GETENV
* (my-getenv "HOME")
"/home/edi"
* (my-getenv "HOM")
NIL
* (my-getenv "HOM" "huh?")
"huh?"
</pre>

You should also note that some of these implementations also provide
the ability to <em>set</em> these variables. These include ECL
(<code>SI:SETENV</code>) and AllegroCL, LispWorks, and CLISP where
you can use the functions from above together with
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/m_setf_.htm"><code>SETF</code></A>.
This feature might be important if you want to start subprocesses from
your Lisp environment.
<p>

<a name="accessing-command-line"></a>
<h3>Accessing the command line arguments</h3>

Accessing command line arguments is implementation-specific but it
appears most implementations have a way of getting at them.  <a
href="http://www.sbcl.org">SBCL</a> has the special variable <code>*posix-argv*</code>

<pre> $ sbcl my-command-line-arg </pre>
<p>....</p>
<pre>* *posix-argv*

("sbcl" "my-command-line-arg")
* 
</pre>

More on using this to write standalone Lisp scripts can be found in the <a
href="http://www.sbcl.org/manual/Unix_002dstyle-Command-Line-Protocol.html">SBCL Manual</a><p/>

<a href="http://www.lispworks.com">LispWorks</a> has
<code>system:*line-arguments-list*</code>

<pre>
CL-USER> system:*line-arguments-list*
("/Users/cbrown/Projects/lisptty/tty-lispworks" "-init" "/Users/cbrown/Desktop/lisp/lispworks-init.lisp")
</pre>

<a href="http://www.cons.org/cmucl/">CMUCL</a> has interesting
extensions for <a href="http://common-lisp.net/project/cmucl/doc/cmu-user/unix.html">manipulating the arguments</a>
<p/>
Here's a quick function to return the argument strings list across multiple
implementations:

<pre>
(defun my-command-line ()
  (or 
   #+SBCL *posix-argv*  
   #+LISPWORKS system:*line-arguments-list*
   #+CMU extensions:*command-line-words*
   nil))
</pre>

<a name="fork-cmucl"></a>
<h3>Forking with CMUCL</h3>

Here's a function by Martin Cracauer that'll allow you to compile a
couple of files in parallel with <a
href="http://www.cons.org/cmucl/">CMUCL</a>. It demonstrates how to
use the UNIX <a href="http://www.freebsd.org/cgi/man.cgi?query=fork&apropos=0&sektion=0&manpath=FreeBSD+4.5-RELEASE&format=html"><code>fork</code></a>
system call with this CL implementation.

<pre>
(defparameter *sigchld* 0)

(defparameter *compile-files-debug* 2)

(defun sigchld-handler (p1 p2 p3)
  (when (&gt; 0 *compile-files-debug*)
    (print (list "returned" p1 p2 p3))
    (force-output))
  (decf *sigchld*))

(defun compile-files (files &key (load nil))
  (setq *sigchld* 0)
  (system:enable-interrupt unix:sigchld #'sigchld-handler)
  (do ((f files (cdr f)))
      ((not f))
    (format t "~&process ~d diving for ~a" (unix:unix-getpid)
	    `(compile-file ,(car f)))
    (force-output)
    (let ((pid (unix:unix-fork)))
      (if (/= 0 pid)
	  ;; parent
	  (incf *sigchld*)
	;; child
	(progn
	  (compile-file (car f) :verbose nil :print nil)
	  (unix:unix-exit 0)))))
  (do () ((= 0 *sigchld*))
    (sleep 1)
    (when (&gt; 0 *compile-files-debug*)
      (format t "~&process ~d still waiting for ~d childs"
	      (unix:unix-getpid) *sigchld*)))
  (when (&gt; 0 *compile-files-debug*)
    (format t "~&finished"))
  (when load
    (do ((f files (cdr f)))
	((not f))
      (load (compile-file-pathname (car f))))))
</pre>

<hr>
<a href="license.html">Copyright</a> &copy; 2002-2007 The Common Lisp Cookbook Project
<address>http://cl-cookbook.sourceforge.net/</address>
<br>$Header$
</body> </html>
