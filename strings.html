<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<!-- 	$Header$	 -->
<html> <head>
<title>The Common Lisp Cookbook - Strings</title>
<style type="text/css">
pre { margin-left:20px; background-color:#e0e0e0 }
</style>

</head>

<body bgcolor=white>

<h1><a href="index.html">The Common Lisp Cookbook</a> - Strings</h1>


<h2>Contents</h2>
<ul>
<li><a href="#substrings">Accessing Substrings</a>
<li><a href="#chars">Accessing Individual Characters</a>
<li><a href="#manip">Manipulating Parts of a String</a>
<li><a href="#concat">Concatenating Strings</a>
<li><a href="#process">Processing a String One Character at a Time</a>
<li><a href="#reverse">Reversing a String by Word or Character</a>
<li><a href="#case">Controlling Case</a>
<li><a href="#trim">Trimming Blanks from the Ends of a String</a>
<li><a href="#symbols">Converting between Symbols and Strings</a>
<li><a href="#conv-chars">Converting between Characters and Strings</a>
<li><a href="#find-el">Finding an Element of a String</a>
<li><a href="#find-sub">Finding a Substring of a String</a>
<li><a href="#number">Converting a String to a Number</a>
<li><a href="#conv-number">Converting a Number to a String</a>
<li><a href="#compare">Comparing Strings</a>
</ul>

The most important thing to know about strings in Common Lisp is
probably that they are arrays and thus also sequences. This implies
that <em>all</em> concepts that are applicable to arrays and sequences
also apply to strings. If you can't find a particular string function,
make sure you've also searched for the more general array or sequence
functions. We'll only cover a fraction of what can be done with and to
strings here.


<a name="substrings"></a>
<h3>Accessing Substrings</h3>

As a string is a sequence, you can access substrings with the 
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_subseq.htm"><code>SUBSEQ</code></a>
function. The index into the string is, as always, zero-based. The
third, optional, argument is the index of the first character which is
not a part of the substring, it is <em>not</em> the length of the
substring. 
  
<pre>
* (defparameter *my-string* (string "Groucho Marx"))
*MY-STRING*
* (subseq *my-string* 8)
"Marx"
* (subseq *my-string* 0 7)
"Groucho"
* (subseq *my-string* 1 5)
"rouc"
</pre>
  
You can also manipulate the substring if you use <code>SUBSEQ</code>
together with <a href="http://www.lispworks.com/documentation/HyperSpec/Body/m_setf_.htm"><code>SETF</code></A>.
  
<pre>
* (defparameter *my-string* (string "Harpo Marx"))
*MY-STRING*
* (subseq *my-string* 0 5)
"Harpo"
* (setf (subseq *my-string* 0 5) "Chico")
"Chico"
* *my-string*
"Chico Marx"
</pre>
  
But note that the string isn't &quot;stretchable&quot;. To cite from the
<a href="http://www.lispworks.com/documentation/HyperSpec/index.html">HyperSpec</a>:
&quot;If the subsequence and the new sequence are not of 
equal length, the shorter length determines the number of elements
that are replaced.&quot; For example:
  
<pre>
* (defparameter *my-string* (string "Karl Marx"))
*MY-STRING*
* (subseq *my-string* 0 4)
"Karl"
* (setf (subseq *my-string* 0 4) "Harpo")
"Harpo"
* *my-string*
"Harp Marx"
* (subseq *my-string* 4)
" Marx"
* (setf (subseq *my-string* 4) "o Marx")
"o Marx"
* *my-string*
"Harpo Mar"
</pre>


<a name="chars"></a>
<h3>Accessing Individual Characters</h3>

You can use the function <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_char_.htm"><code>CHAR</code></a>
to access individual characters of a string. <code>CHAR</code> can
also be used in conjunction with <code>SETF</code>.
  
<pre>
* (defparameter *my-string* (string "Groucho Marx"))
*MY-STRING*
* (char *my-string* 11)
#\x
* (char *my-string* 7)
#\Space
* (char *my-string* 6)
#\o
* (setf (char *my-string* 6) #\y)
#\y
* *my-string*
"Grouchy Marx"
</pre>

Note that there's also <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_char_.htm"><code>SCHAR</code></A>.
If efficiency is important, <code>SCHAR</code> can be a bit faster 
where appropriate.
<p>

Because strings are arrays and thus sequences, you can also use the more
generic functions <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_aref.htm"><code>AREF</code></a>
and <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_elt.htm"><code>ELT</code></a>
(which are more general while <code>CHAR</code> might be implemented
more efficiently). 

<pre>
* (defparameter *my-string* (string "Groucho Marx"))
*MY-STRING*
* (aref *my-string* 3)
#\u
* (elt *my-string* 8)
#\M
</pre>

Each character in a string has an integer code.  The range of
recognized codes and Lisp's ability to print them is directed related
to your implementation's character set support, e.g. ISO-8859-1, or
Unicode.  Here are some examples in <a
href="http://www.sbcl.org">SBCL</a> of UTF-8 which encodes characters
as 1 to 4 8 bit bytes.  The first example shows a character outside
the first 128 chars, or what is considered the normal Latin character
set.  The second example shows a multibyte encoding (beyond the value
255).  Notice the Lisp reader can round-trip characters by name.

<pre>
* (stream-external-format *standard-output*)

:UTF-8
* (code-char 200)

#\LATIN_CAPITAL_LETTER_E_WITH_GRAVE
* (char-code #\LATIN_CAPITAL_LETTER_E_WITH_GRAVE)

200
* (code-char 1488)
#\HEBREW_LETTER_ALEF

* (char-code #\HEBREW_LETTER_ALEF)
1488
</pre>

Check out the <a href="http://en.wikipedia.org/wiki/UTF-8"><code>UTF-8
Wikipedia article</code></a> for the range of supported characters and their encodings.

<a name="manip"></a>
<h3>Manipulating Parts of a String</h3>

There's a slew of (sequence) functions that can be used to
manipulate a string and we'll only provide some
examples here. See the <a href="http://www.lispworks.com/documentation/HyperSpec/Body/c_sequen.htm">sequences
dictionary</a> in the HyperSpec for more.

<pre>
* (remove #\o "Harpo Marx")
"Harp Marx"
* (remove #\a "Harpo Marx")
"Hrpo Mrx"
* (remove #\a "Harpo Marx" :start 2)
"Harpo Mrx"
* (remove-if #'upper-case-p "Harpo Marx")
"arpo arx"
* (substitute #\u #\o "Groucho Marx")
"Gruuchu Marx"
* (substitute-if #\_ #'upper-case-p "Groucho Marx")
"_roucho _arx"
* (defparameter *my-string* (string "Zeppo Marx"))
*MY-STRING*
* (replace *my-string* "Harpo" :end1 5)
"Harpo Marx"
* *my-string*
"Harpo Marx"
</pre>

Another function that can be frequently used (but not part of the ANSI standard)
is <code>replace-all</code>. This function provides an easy functionality
for search/replace operations on a string, by returning a new string 
in which all the occurences of the 'part' in string is replaced with 
'replacement'".

<pre>
* (replace-all "Groucho Marx Groucho" "Groucho" "ReplacementForGroucho")
"ReplacementForGroucho Marx ReplacementForGroucho"
</pre>

One of the implementations of <code>replace-all</a> is as follows:

<pre>
(defun replace-all (string part replacement &key (test #'char=))
"Returns a new string in which all the occurences of the part 
is replaced with replacement."
    (with-output-to-string (out)
      (loop with part-length = (length part)
            for old-pos = 0 then (+ pos part-length)
            for pos = (search part string
                              :start2 old-pos
                              :test test)
            do (write-string string out
                             :start old-pos
                             :end (or pos (length string)))
            when pos do (write-string replacement out)
            while pos))) 
</pre>

However, bear in mind that the above code is not optimized for
long strings; if you intend to perform such an operation on very
long strings, files, etc. please consider using 
<a href="http://www.weitz.de/cl-ppcre/">cl-ppcre</a> regular expressions
and string processing library which is heavily optimized.

<a name="concat"></a>
<h3>Concatenating Strings</h3>

The name says it all: <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_concat.htm"><code>CONCATENATE</code></a>
is your friend. Note that this a generic sequence function and you
have to provide the result type as the first argument.

<pre>
* (concatenate 'string "Karl" " " "Marx")
"Karl Marx"
* (concatenate 'list "Karl" " " "Marx")
(#\K #\a #\r #\l #\Space #\M #\a #\r #\x)
</pre>

If you have to construct a string out of many parts, all of these calls
to <code>CONCATENATE</code> seem wasteful, though. There are at least
three other good ways to construct a string piecemeal, depending on
what exactly your data is.

If you build your string one character at a time, make it an
<em>adjustable</em> VECTOR (a one-dimensional <a
href="http://www.lispworks.com/documentation/HyperSpec/Body/15_.htm">ARRAY</a>)
of type character with a fill-pointer of zero, then use <a
href="http://www.lispworks.com/documentation/HyperSpec/Body/f_vec_ps.htm"><code>VECTOR-PUSH-EXTEND</code></a>
on it.  That way, you can also give hints to the system if you can
estimate how long the string will be. (See the optional third argument
to <code>VECTOR-PUSH-EXTEND</code>.)

<pre>
* (defparameter *my-string* (make-array 0
                                        :element-type 'character
                                        :fill-pointer 0
                                        :adjustable t))
*MY-STRING*
* *my-string*
""
* (dolist (char '(#\Z #\a #\p #\p #\a))
    (vector-push-extend char *my-string*))
NIL
* *my-string*
"Zappa"
</pre>

If the string will be constructed out of (the printed
representations of) arbitrary objects, (symbols, numbers,
characters, strings, ...), you can use <a
href="http://www.lispworks.com/documentation/HyperSpec/Body/f_format.htm"><code>FORMAT</code></a>
with an output stream argument of <code>NIL</code>. This directs
<code>FORMAT</code> to return the indicated output as a string.

<pre>
* (format nil "This is a string with a list ~A in it"
          '(1 2 3))
"This is a string with a list (1 2 3) in it"
</pre>

We can use the looping constructs of the <code>FORMAT</code> mini language to
emulate <code>CONCATENATE</code>.

<pre>
* (format nil "The Marx brothers are:~{ ~A~}."
          '("Groucho" "Harpo" "Chico" "Zeppo" "Karl"))
"The Marx brothers are: Groucho Harpo Chico Zeppo Karl."
</pre>

<code>FORMAT</code> can do a lot more processing but it has a
relatively arcane syntax. After this last example, you can find the
details in <a href="http://www.lispworks.com/documentation/HyperSpec/Body/22_c.htm">the
CLHS section about formatted output</a>. 

<pre>
* (format nil "The Marx brothers are:~{ ~A~^,~}."
          '("Groucho" "Harpo" "Chico" "Zeppo" "Karl"))
"The Marx brothers are: Groucho, Harpo, Chico, Zeppo, Karl."
</pre>

Another way to create a string out of the printed representation of
various object is using <a href="http://www.lispworks.com/documentation/HyperSpec/Body/m_w_out_.htm"><code>WITH-OUTPUT-TO-STRING</code></a>.
The value of this handy macro is a string containing everything that
was output to the string stream within the body to the macro. This
means you also have the full power of 
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_format.htm"><code>FORMAT</code></a>
at your disposal, should you need it.

<pre>
* (with-output-to-string (stream)
    (dolist (char '(#\Z #\a #\p #\p #\a #\, #\Space))
      (princ char stream))
    (format stream "~S - ~S" 1940 1993))
"Zappa, 1940 - 1993"
</pre>


<a name="process"></a>
<h3>Processing a String One Character at a Time</h3>

Use the <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_map.htm"><code>MAP</code></a>
function to process a string one character at a time.

<pre>
* (defparameter *my-string* (string "Groucho Marx"))
*MY-STRING*
* (map 'string #'(lambda (c) (print c)) *my-string*)
#\G 
#\r 
#\o 
#\u 
#\c 
#\h 
#\o 
#\Space 
#\M 
#\a 
#\r 
#\x 
"Groucho Marx"
</pre>

Or do it with <a href="http://www.lispworks.com/documentation/HyperSpec/Body/06_a.htm"><code>LOOP</code></a>.

<pre>
* (loop for char across "Zeppo"
        collect char)
(#\Z #\e #\p #\p #\o)
</pre>


<a name="reverse"></a>
<h3>Reversing a String by Word or Character</h3>

Reversing a string by character is easy using the built-in 
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_revers.htm"><code>REVERSE</code></a>
function (or its <a href="http://www.lispworks.com/documentation/HyperSpec/Body/26_glo_d.htm#destructive">destructive</a> counterpart <code>NREVERSE</code>).

<pre>
*(defparameter *my-string* (string "DSL"))
*MY-STRING*
* (reverse *my-string*)
"LSD"
</pre>

There's no one-liner in CL to reverse a string by word (like you
would do it in Perl with <code>split</code> and
<code>join</code>). You either have to use function from an external
library like <a href="http://ww.telent.net/cliki/SPLIT-SEQUENCE"><code>SPLIT-SEQUENCE</code></a> or
you have to roll your own solution. Here's an attempt:

<pre>
* (defun split-by-one-space (string)
    "Returns a list of substrings of string
divided by ONE space each.
Note: Two consecutive spaces will be seen as
if there were an empty string between them."
    (loop for i = 0 then (1+ j)
          as j = (position #\Space string :start i)
          collect (subseq string i j)
          while j))
SPLIT-BY-ONE-SPACE
* (split-by-one-space "Singing in the rain")
("Singing" "in" "the" "rain")
* (split-by-one-space "Singing in the  rain")
("Singing" "in" "the" "" "rain")
* (split-by-one-space "Cool")
("Cool")
* (split-by-one-space " Cool ")
("" "Cool" "")
* (defun join-string-list (string-list)
    "Concatenates a list of strings
and puts spaces between the elements."
    (format nil "~{~A~^ ~}" string-list))
JOIN-STRING-LIST
* (join-string-list '("We" "want" "better" "examples"))
"We want better examples"
* (join-string-list '("Really"))
"Really"
* (join-string-list '())
""
* (join-string-list
   (nreverse
    (split-by-one-space
     "Reverse this sentence by word")))
"word by sentence this Reverse"
</pre>


<a name="case"></a>
<h3>Controlling Case</h3>

Common Lisp has a couple of <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_stg_up.htm">functions
to control the case of a string</a>.

<pre>
* (string-upcase "cool")
"COOL"
* (string-upcase "Cool")
"COOL"
* (string-downcase "COOL")
"cool"
* (string-downcase "Cool")
"cool"
* (string-capitalize "cool")
"Cool"
* (string-capitalize "cool example")
"Cool Example"
</pre>

These functions take <code>:START</code> and <code>:END</code> keyword
arguments so you can optionally only manipulate a part of the
string. They also have destructive counterparts whose names starts
with &quot;N&quot;.

<pre>
* (string-capitalize "cool example" :start 5)
"cool Example"
* (string-capitalize "cool example" :end 5)
"Cool example"
* (defparameter *my-string* (string "BIG"))
*MY-STRING*
* (defparameter *my-downcase-string* (nstring-downcase *my-string*))
*MY-DOWNCASE-STRING*
* *my-downcase-string*
"big"
* *my-string*
"big"
</pre>

Note this potential caveat: According to the HyperSpec, "for
STRING-UPCASE, STRING-DOWNCASE, and STRING-CAPITALIZE, <em>string</em>
is not modified. However, if no characters in string require
conversion, the result may be either <em>string</em> or a copy of it,
at the implementation's discretion." This implies the last result in
the following example is implementation-dependent - it may either be
&quot;BIG&quot; or &quot;BUG&quot;. If you want to be sure, use <a
href="http://www.lispworks.com/documentation/HyperSpec/Body/f_cp_seq.htm"><code>COPY-SEQ</code></a>.

<pre>
* (defparameter *my-string* (string "BIG"))
*MY-STRING*
* (defparameter *my-upcase-string* (string-upcase *my-string*))
*MY-UPCASE-STRING*
* (setf (char *my-string* 1) #\U)
#\U
* *my-string*
"BUG"
* *my-upcase-string*
"BIG"
</pre>


<a name="trim"></a>
<h3>Trimming Blanks from the Ends of a String</h3>

Not only can you trim blanks, but you can get rid of arbitary characters.
The functions <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_stg_tr.htm"><code>STRING-TRIM</code>, <code>STRING-LEFT-TRIM</code> and <code>STRING-RIGHT-TRIM</code></a>
return a substring of their second argument where all characters that
are in the first argument are removed  off the beginning and/or
the end. The first argument can be any <em>sequence</em> of characters.

<pre>
* (string-trim " " " trim me ")
"trim me"
* (string-trim " et" " trim me ")
"rim m"
* (string-left-trim " et" " trim me ")
"rim me "
* (string-right-trim " et" " trim me ")
" trim m"
* (string-right-trim '(#\Space #\e #\t) " trim me ")
" trim m"
* (string-right-trim '(#\Space #\e #\t #\m) " trim me ")
</pre>

Note: The caveat mentioned in the section about <a
href="#case">Controlling Case</a> also applies here.


<a name="symbols"></a>
<h3>Converting between Symbols and Strings</h3>

The function <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_intern.htm"><code>INTERN</code></a>
will &quot;convert&quot; a string to a symbol. Actually, it will check
whether the symbol denoted by the string (its first argument) is
already accessible in the package (its second, optional, argument
which defaults to the current package) and enter it, if
necessary, into this package. It is beyond the scope of this chapter
to explain all the concepts involved and to address the second return
value of this function. See <a
href="http://www.lispworks.com/documentation/HyperSpec/Body/11_.htm">the
CLHS chapter about packages</a> for details.
<p>

Note that the case of the string is relevant.

<pre>
* (in-package "COMMON-LISP-USER")
#&lt;The COMMON-LISP-USER package, 35/44 internal, 0/9 external&gt;
* (intern "MY-SYMBOL")
MY-SYMBOL
NIL
* (intern "MY-SYMBOL")
MY-SYMBOL
:INTERNAL
* (export 'MY-SYMBOL)
T
* (intern "MY-SYMBOL")
MY-SYMBOL
:EXTERNAL
* (intern "My-Symbol")
|My-Symbol|
NIL
* (intern "MY-SYMBOL" "KEYWORD")
:MY-SYMBOL
NIL
* (intern "MY-SYMBOL" "KEYWORD")
:MY-SYMBOL
:EXTERNAL
</pre>

To do the opposite, convert from a symbol to a string, use 
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_symb_2.htm">SYMBOL-NAME</a> or <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_string.htm"><code>STRING</code></a>.

<pre>
* (symbol-name 'MY-SYMBOL)
"MY-SYMBOL"
* (symbol-name 'my-symbol)
"MY-SYMBOL"
* (symbol-name '|my-symbol|)
"my-symbol"
* (string 'howdy)
"HOWDY"
</pre>


<a name="conv-chars"></a>
<h3>Converting between Characters and Strings</h3>

You can use <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_coerce.htm"><code>COERCE</code></A>
to convert a string of length 1 to a character. You can also use
<code>COERCE</code> to convert any sequence of characters into a
string. You can <em>not</em> use <code>COERCE</code> to convert a
character to a string, though - you'll have to use 
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_string.htm"><code>STRING</code></a>
instead.
  
<pre>
* (coerce "a" 'character)
#\a
* (coerce (subseq "cool" 2 3) 'character)
#\o
* (coerce "cool" 'list)
(#\c #\o #\o #\l)
* (coerce '(#\h #\e #\y) 'string)
"hey"
* (coerce (nth 2 '(#\h #\e #\y)) 'character)
#\y
* (defparameter *my-array* (make-array 5 :initial-element #\x))
*MY-ARRAY*
* *my-array*
#(#\x #\x #\x #\x #\x)
* (coerce *my-array* 'string)
"xxxxx"
* (string 'howdy)
"HOWDY"
* (string #\y)
"y"
* (coerce #\y 'string)
#\y can't be converted to type STRING.
   [Condition of type SIMPLE-TYPE-ERROR]
</pre>

<a name="find-el"></a>
<h3>Finding an Element of a String</h3>

Use <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_find_.htm"><code>FIND</code></a>,
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_pos_p.htm"><code>POSITION</code></a>,
and their <code>-IF</code> counterparts to find characters in a string.

<pre>
* (find #\t "The Hyperspec contains approximately 110,000 hyperlinks." :test #'equal)
#\t
* (find #\t "The Hyperspec contains approximately 110,000 hyperlinks." :test #'equalp)
#\T
* (find #\z "The Hyperspec contains approximately 110,000 hyperlinks." :test #'equalp)
NIL
* (find-if #'digit-char-p "The Hyperspec contains approximately 110,000 hyperlinks.")
#\1
* (find-if #'digit-char-p "The Hyperspec contains approximately 110,000 hyperlinks." :from-end t)
#\0
* (position #\t "The Hyperspec contains approximately 110,000 hyperlinks." :test #'equal)
17
* (position #\t "The Hyperspec contains approximately 110,000 hyperlinks." :test #'equalp)
0
* (position-if #'digit-char-p "The Hyperspec contains approximately 110,000 hyperlinks.")
37
* (position-if #'digit-char-p "The Hyperspec contains approximately 110,000 hyperlinks." :from-end t)
43
</pre>

Or use <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_countc.htm"><code>COUNT</code></a>
and friends to count characters in a string.

<pre>
* (count #\t "The Hyperspec contains approximately 110,000 hyperlinks." :test #'equal)
2
* (count #\t "The Hyperspec contains approximately 110,000 hyperlinks." :test #'equalp)
3
* (count-if #'digit-char-p "The Hyperspec contains approximately 110,000 hyperlinks.")
6
* (count-if #'digit-char-p "The Hyperspec contains approximately 110,000 hyperlinks." :start 38)
5
</pre>


<a name="find-sub"></a>
<h3>Finding a Substring of a String</h3>

The function <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_search.htm"><code>SEARCH</code></a>
can find substrings of a string.

<pre>
* (search "we" "If we can't be free we can at least be cheap")
3
* (search "we" "If we can't be free we can at least be cheap" :from-end t)
20
* (search "we" "If we can't be free we can at least be cheap" :start2 4)
20
* (search "we" "If we can't be free we can at least be cheap" :end2 5 :from-end t)
3
* (search "FREE" "If we can't be free we can at least be cheap")
NIL
* (search "FREE" "If we can't be free we can at least be cheap" :test #'char-equal)
15
</pre>


<a name="number"></a>
<h3>Converting a String to a Number</h3>

CL provides the <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_parse_.htm"><code>PARSE-INTEGER</code></a>
to convert a string representation of an integer to the corresponding
numeric value. The second return value is the index into the string
where the parsing stopped.

<pre>
* (parse-integer "42")
42
2
* (parse-integer "42" :start 1)
2
2
* (parse-integer "42" :end 1)
4
1
* (parse-integer "42" :radix 8)
34
2
* (parse-integer " 42 ")
42
3
* (parse-integer " 42 is forty-two" :junk-allowed t)
42
3
* (parse-integer " 42 is forty-two")

Error in function PARSE-INTEGER:
   There's junk in this string: " 42 is forty-two".
</pre>

<code>PARSE-INTEGER</code> doesn't understand radix specifiers like
<code>#X</code>, nor is there a built-in function to parse other
numeric types. You could use 
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_rd_fro.htm"><code>READ-FROM-STRING</code></a>
in this case, but be aware that the full <a href="http://www.lispworks.com/documentation/HyperSpec/Body/02_b.htm">reader</a>
is in effect if you're using this function.

<pre>
* (read-from-string "#X23")
35
4
* (read-from-string "4.5")
4.5
3
* (read-from-string "6/8")
3/4
3
* (read-from-string "#C(6/8 1)")
#C(3/4 1)
9
* (read-from-string "1.2e2")
120.00001
5
* (read-from-string "symbol")
SYMBOL
6
* (defparameter *foo* 42)
*FOO*
* (read-from-string "#.(setq *foo* \"gotcha\")")
"gotcha"
23
* *foo*
"gotcha"
</pre>

<a name="conv-number"></a>
<h3>Converting a Number to a String</h3>

The general function <a
href="http://www.lispworks.com/documentation/HyperSpec/Body/f_wr_to_.htm"><code>WRITE-TO-STRING</code></A>
or one of its simpler variants <code>PRIN1-TO-STRING</code> or
<code>PRINC-TO-STRING</code> may be used to convert a number to a
string.  With <code>WRITE-TO-STRING</code>, the :base keyword argument
may be used to change the output base for a single call.  To change
the output base globally, set <code>*print-base*</code> which defaults
to 10.  Remember in Lisp, rational numbers are represented as
quotients of two integers even when converted to strings.

<pre>
* (write-to-string 250)
"250"
* (write-to-string 250.02)
"250.02"
* (write-to-string 250 :base 5)
"2000"
* (write-to-string (/ 1 3))
"1/3"
* 
</pre>

<a name="compare"></a>
<h3>Comparing Strings</h3>

The general functions
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_equal.htm"><code>EQUAL</code></a>
and <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_equalp.htm"><code>EQUALP</code></a>
can be used to test whether two strings are equal. The strings are
compared element-by-element, either in a case-sensitive manner
(<code>EQUAL</code>) or not (<code>EQUALP</code>). There's also a
bunch of <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_stgeq_.htm">string-specific 
comparison functions</a>. You'll want to use these if you're deploying
<em>implementation-defined</em> attributes of characters. Check your
vendor's documentation in this case.
<p>

Here are a few examples. Note that all functions that test for
inequality return the position of the first mismatch as a 
<a href="http://www.lispworks.com/documentation/HyperSpec/Body/26_glo_g.htm#generalized_boolean">generalized
boolean</a>. You can also use the generic sequence function <a href="http://www.lispworks.com/documentation/HyperSpec/Body/f_mismat.htm"><code>MISMATCH</code></a>
if you need more versatility.

<pre>
* (string= "Marx" "Marx")
T
* (string= "Marx" "marx")
NIL
* (string-equal "Marx" "marx")
T
* (string< "Groucho" "Zeppo")
0
* (string< "groucho" "Zeppo")
NIL
* (string-lessp "groucho" "Zeppo")
0
* (mismatch "Harpo Marx" "Zeppo Marx" :from-end t :test #'char=)
3
</pre>


<hr>
<a href="license.html">Copyright</a> &copy; 2002-2007 The Common Lisp Cookbook Project
<address>http://cl-cookbook.sourceforge.net/</address>
<br>$Header$
</body> </html>
